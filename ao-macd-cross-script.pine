//@version=4
// Awesome Oscillator (AO) and MACD cross by samgozman (https://github.com/samgozman)
// 
// ++++++++++ WHAT IT IS ??? ++++++++++ //
// An oscillator that uses a signal line crossing MACD line as an observation point. 
// The intersection of Awesome Oscillator is used as a confirmation signal to buy or sell.
// The intersection of MACD and AO histograms (yellow dot) is calculated as a potential point for closing a trade.
// 
// ++++++++++ HOW TO READ ++++++++++ //
// Long: green dot / plus sign, Short: red dot / plus sign. Yellow dot - exit point.
// Area filled with red/green color is MACD histogram.
// Area with red/green lines is Awesome Oscillator histogram.
// Blue thick line is MACD main line. Orange line - MACD signal line.
// Blueish filled area is a Rapid reaction zone.
// 
// ++++++++++ HOW TO USE ++++++++++ //
// * Conservative strategy (lower risk) - recommended *
// 1) Look at the intersection of the MACD lines marked with a green/red dot.
// 2) Wait for the confirmation (green/red plus sign on the oscillator axis). This will be your entry point.
// 3) The intersection of MACD and AO histograms (yellow dot) is your exit point.
// 
// * Aggressive strategy (higher risk) *
// 1) Look at the intersection of the MACD lines marked with a green/red dot.
// 2) Set up a rapid reaction zone so that the points of MACD intersections on your timeframe did not get into it. (If possible)
// 3) When the MACD lines cross the border of the rapid zone - open a trade. The further the lines are from each other, the better.
// 4) Closing the trade is up to you. You can close when the yellow dot forms, or based on common Awesome Oscillator tactics.
// 
// ++++++++++ Warning: The script is provided for educational purposes only. ++++++++++ //

study(title="Awesome Oscillator & MACD Cross", shorttitle="AO & MACD Cross", overlay=false)

// ++++++++++ INPUTS ++++++++++ //
source = input(title="Source for MACD", type=input.source, defval=close)
fastLength = input(12, title="Fast EMA length", minval=1)
slowLength = input(26, title="Slow EMA length", minval=1)
signalLength = input(9, title="Signal EMA length", minval=1)

risk = input(title="Aggressive strategy (risky)", type=input.bool, defval=false)
rapidAreaTop = input(0.15, title="Rapid area high limit", type=input.float, minval=0, maxval=1)
rapidAreaBottom = input(-0.15, title="Rapid area low limit", type=input.float, maxval=0, minval=-1)

// ++++++++++ LOGIC - AO ++++++++++ //
ao = (sma(hl2, 5) - sma(hl2, 34)) / 2

// ++++++++++ LOGIC - MACD ++++++++++ //
fastEMA = ema(source, fastLength)
slowEMA = ema(source, slowLength)
macd = fastEMA - slowEMA
signal = sma(macd, signalLength)
hist = (macd - signal) * 2

// ++++++++++ PLOT - Rapid response area ++++++++++ //

rapidBandTop = hline(risk ? rapidAreaTop : na, title="Rapid signal area top", linestyle=hline.style_dotted)
rapidBandBottom = hline(risk ? rapidAreaBottom : na, title="Rapid signal area bottom", linestyle=hline.style_dotted)
fill(rapidBandTop, rapidBandBottom, title="Rapid signal area", color=color.blue, transp=90)

// ++++++++++ PLOT - AO ++++++++++ //
plot(ao, title="Awesome oscillator", color=change(ao) <= 0 ? color.red : color.green, style=plot.style_histogram)

// ++++++++++ PLOT - MACD ++++++++++ //
plot(hist, title="MACD Histogram", color=hist > 0 ? color.green : color.red, style=plot.style_area, transp=75)
plot(macd, title="MACD main line", color=color.blue, linewidth=2)
plot(signal, title="MACD signal line", color=color.orange, linewidth=1)

// ++++++++++ PLOT - Cross marks ++++++++++ //
plot(cross(macd, signal) ? signal : na, title="MACD cross", style=plot.style_circles, linewidth=3, color=macd > signal ? color.green : color.red)
plot(not risk ? cross(ao, 0) ? 0 : na : na, title="AO cross", style=plot.style_cross, linewidth=3, color=ao > 0 ? color.green : color.red)
plot(risk ? crossunder(macd, rapidAreaTop) ? rapidAreaTop : na : na, title="MACD Risk cross", style=plot.style_cross, linewidth=3, color= color.red)
plot(risk ? crossover(macd, rapidAreaBottom) ? rapidAreaBottom : na : na, title="MACD Risk cross", style=plot.style_cross, linewidth=3, color= color.green)
plot(cross(ao, hist) ? ao : na, title="AO cross Hist (Close the deal)", style=plot.style_circles, linewidth=2, color=color.yellow)
